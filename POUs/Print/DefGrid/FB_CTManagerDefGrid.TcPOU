<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_CTManagerDefGrid" Id="{64e24f37-1762-4987-94b6-1c2daabf0872}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_CTManagerDefGrid EXTENDS FB_CTManager
VAR
  fbContainerInstances    : ARRAY[0..nC_MAX_CTS-1] OF FB_ContainerDefGrid;   // NOTE: 0 based array like base in library
  nCtr, nIndx : UDINT;
  _LogLevelCTs : E_LogSeverity := E_LogSeverity.Info;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// call CT manager base
SUPER^();

// call specific CT implementation
nCtr := 0;
WHILE nCtr < nListCount DO
  nIndx := ((nListTail + nCtr) MOD nC_MAX_CTS);    // calculate element index in list
  
  // cyclic call of CT body
  fbContainerInstances[nIndx]();
  
  nCtr := nCtr + 1;
END_WHILE

updateLogLevelsCTs();
]]></ST>
    </Implementation>
    <Method Name="AbortCts" Id="{887d2e55-f283-4461-92bd-fe8efb68fdd3}">
      <Declaration><![CDATA[(*
  Abort CTs
*)
METHOD AbortCts : BOOL
VAR_INPUT
  iCT : I_CT;     // reference CT 
  nSel : INT;     // selection: -1=all older (created before reference), 0=current only, 1=all newer (created after reference)
  Src : REFERENCE TO T_ErrSrc;
END_VAR
VAR
  iIterCt : I_CT;
  pCtSelected : POINTER TO FB_ContainerDefGrid;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// WARNING: This function is NOT tested!!! 
//    TODO: remove this comments when tested

IF nSel = 0 THEN
  // Abort referenced CT
  pCtSelected := GetContainerInstance(iCT:=iCT);
  IF pCtSelected <> 0 THEN
    pCtSelected^.AbortDeformation(sReason:=F_Concat2('CTM Abort CTs Sel=', TO_STRING(nSel)), Src:=Src, bHideLog:=FALSE);
  END_IF

ELSIF nSel < 0 THEN
  // Abort previous
  iIterCt := iCT;
  WHILE iIterCt.pListPrev <> 0 DO
    // get instance and abort
    pCtSelected := GetContainerInstance(iCT:=iIterCt);
    IF pCtSelected <> 0 THEN
      pCtSelected^.AbortDeformation(sReason:=F_Concat2('CTM Abort CTs Sel=', TO_STRING(nSel)), Src:=Src, bHideLog:=FALSE);
    END_IF
    // select prev
    iIterCt := iIterCt.pListPrev;
  END_WHILE

ELSIF nSel > 0 THEN  
  // Abort next
  iIterCt := iCT;
  WHILE iIterCt.pListNext <> 0 DO
    // get instance and abort
    pCtSelected := GetContainerInstance(iCT:=iIterCt);
    IF pCtSelected <> 0 THEN
      pCtSelected^.AbortDeformation(sReason:=F_Concat2('CTM Abort CTs Sel=', TO_STRING(nSel)), Src:=Src, bHideLog:=FALSE);
    END_IF
    // select next
    iIterCt := iIterCt.pListNext;
  END_WHILE
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetContainerInstance" Id="{4019ff74-9ad2-43f1-90de-a88d411277f4}">
      <Declaration><![CDATA[(*
  Get specific container reference (pointer) from container interface.
*)
METHOD PUBLIC GetContainerInstance : POINTER TO FB_ContainerDefGrid
VAR_INPUT
  iCT              : I_CT;          (*container at triggered event position*)
END_VAR
VAR
  nCtSel             : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF iCT = 0 THEN LogError(Msg:='GetContainerInstance invalid container'); RETURN; END_IF
nCtSel := iCT.pCT^.nMemIndex;
IF nCtSel < nC_MAX_CTS THEN
  GetContainerInstance := ADR( fbContainerInstances[nCtSel] );
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Init" Id="{322a3013-e920-45b4-8f41-4730c09f6a49}">
      <Declaration><![CDATA[METHOD Init
VAR
  nCtr      : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_LogLevelCTs := E_LogSeverity.Info;

// init FB_CTManager(base) list (interface I_CT) with specific Container instances (FBs) 
FOR nCtr := 0 TO nC_MAX_CTS-1 DO
  iContainerList[nCtr] := fbContainerInstances[nCtr];
  fbContainerInstances[nCtr].LogLevel := _LogLevelCTs;
END_FOR

// call parent init
SUPER^.InitBase(aName:='CTM', aUnit:='SubPx', bAutoDelete:=TRUE);]]></ST>
      </Implementation>
    </Method>
    <Property Name="sCTDefGridInstancesPath" Id="{b4d88d3c-ac8f-4c1b-9101-ee6ed3ea35b7}">
      <Declaration><![CDATA[PROPERTY sCTDefGridInstancesPath : STRING]]></Declaration>
      <Get Name="Get" Id="{d342d67e-171a-43aa-9516-1ffc59a514cc}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[// build path tho one image deformation fb
// e.g. PRG_PRINT.fbCTM.fbContainerInstances[%id%].fBImageDeformation where %id% will be resolved later in the pyhton script.
//      array in plc lower bound is index 0
sCTDefGridInstancesPath := F_CONCAT2(THIS^.InstanceName, '.fbContainerInstances[%id%].fBImageDeformation');]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="updateLogLevelsCTs" Id="{c5b2038e-8748-4e32-b316-bccf513e122a}">
      <Declaration><![CDATA[METHOD PRIVATE updateLogLevelsCTs
VAR_INST
  _LogLvl_mem  : E_LogSeverity;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// logLevel update from manager to all containers
IF _LogLevelCTs <> _LogLvl_mem THEN 
  nCtr := 0;
  WHILE nCtr < nC_MAX_CTS DO
    fbContainerInstances[nCtr].LogLevel := _LogLevelCTs;
    nCtr := nCtr + 1;
  END_WHILE
END_IF

_LogLvl_mem := _LogLevelCTs;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_CTManagerDefGrid">
      <LineId Id="105" Count="14" />
      <LineId Id="44" Count="0" />
    </LineIds>
    <LineIds Name="FB_CTManagerDefGrid.AbortCts">
      <LineId Id="3" Count="22" />
      <LineId Id="52" Count="0" />
      <LineId Id="27" Count="12" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_CTManagerDefGrid.GetContainerInstance">
      <LineId Id="16" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="18" Count="0" />
    </LineIds>
    <LineIds Name="FB_CTManagerDefGrid.Init">
      <LineId Id="26" Count="1" />
      <LineId Id="16" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_CTManagerDefGrid.sCTDefGridInstancesPath.Get">
      <LineId Id="6" Count="1" />
      <LineId Id="10" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_CTManagerDefGrid.updateLogLevelsCTs">
      <LineId Id="3" Count="8" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>