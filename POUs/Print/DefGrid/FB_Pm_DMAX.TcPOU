<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_Pm_DMAX" Id="{bb1b9ded-b502-4468-a5cd-77b9faddac57}" SpecialFunc="None">
    <Declaration><![CDATA[(*
  Managing a production on a DigiRound machine.
*)
FUNCTION_BLOCK FB_Pm_DMAX EXTENDS FB_BasePrint IMPLEMENTS I_PrintStatusClient
VAR
 //references
  _pPU                : POINTER TO FB_PU;               (*PU reference*)
  _pAdsPU             : POINTER TO ST_ADS_PU;
  
  //settings
  _defaultPrintData   : ST_PRINT_SETTING;               (*default PDI data if no CT present*)
  _isVarData          : BOOL; 

  //status
  bPmActive           : BOOL;                           (*a production is active*)
  rfTrig_PmActive     : RF_TRIG;
  lastCreatedCT       : I_CT;                           (*last created container*)

  (*lists*)
  (*--list implementations*)
  prodList            : FB_ListPointerDyn;              (*list fb [pointer to I_CT] - current production elements*)
  _prodMem            : ARRAY[1..lNUM_LIST_SIZE] OF PVOID;   (*list memory for prodList*)
  (*--job trace list*)
  _currJobId          : STRING;                         (*id of current started job*)

  (*internals*)
  _pEcSrc : POINTER TO T_ErrSrc;                                    (*error source definition*)

END_VAR
VAR CONSTANT
  lNUM_LIST_SIZE        : DINT := iCPr_MAX_CONTAINER;   (*number of max. list elements*)
  // other logs
  aC_Dbg_NotReject      : STRING := 'could not rejectNext CT';
  aC_Dbg_InvPtrCT       : STRING := 'invalid pO_ContainerHead';
  aC_Dbg_InvPtrJob      : STRING := 'invalid Job pointer';
  aC_Ddb_GetNext        : STRING := 'getNext from: ';  
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT bInitialized THEN RETURN; END_IF  // ignore

rfTrig_PmActive(CLK:=bPmActive);]]></ST>
    </Implementation>
    <Folder Name="I_PrintStatusClient" Id="{01602ff5-4961-4770-a9e0-df788cccf137}" />
    <Method Name="abortJob" Id="{a4c167dc-2e1c-4cb0-a924-fdde935ac4c1}">
      <Declaration><![CDATA[(*
  Job was stopped. 
  :return: TRUE if some elements were aborted in machine.
*)
METHOD PRIVATE abortJob : BOOL
VAR
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// a job stopped.

// NOTE: not implemented

]]></ST>
      </Implementation>
    </Method>
    <Property Name="bReady" Id="{dcb711e8-cdcf-4c79-a8a4-763acdfbddae}">
      <Declaration><![CDATA[PROPERTY bReady : BOOL]]></Declaration>
      <Get Name="Get" Id="{2fc1a06b-46e5-4f81-97b7-74d963af6889}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[bReady := bPmActive;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="bStarted" Id="{2f7cd73e-0b19-4d2a-86d9-8606ab7d4317}">
      <Declaration><![CDATA[PROPERTY bStarted : BOOL]]></Declaration>
      <Get Name="Get" Id="{b5077a03-1bdd-4036-a284-83025a83fe35}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[bStarted := rfTrig_PmActive.RT;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="bStopped" Id="{1546dc04-e456-4125-9dbb-574cec1e9dea}">
      <Declaration><![CDATA[PROPERTY bStopped : BOOL]]></Declaration>
      <Get Name="Get" Id="{2dbadb30-6efc-4edd-a3a0-df5eadbe4eb0}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[bStopped := rfTrig_PmActive.FT;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="CT_Create" Id="{76995193-cd48-4f19-a717-981d29070032}">
      <Declaration><![CDATA[(*
Create/Build a new container (CT) with next page to be printed.
*)
METHOD CT_Create : BOOL
VAR_INPUT
  sCT            : I_CT;                  // container to produce
END_VAR
VAR 
  _created       : BOOL := FALSE;
  sPage          : ST_PrintPageDef;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// prepare & checks
lastCreatedCT := 0;                      // invalidate last created
IF NOT bInitialized THEN RETURN; END_IF
IF sCT = 0 THEN LogError(aC_Dbg_InvPtrCT); RETURN; END_IF

(* getNext CT *)
IF getNext(sCT := sCT) THEN
  
  // define printer output
  sPage := sCT.sPageDef;
  IF    sPage.lType = EN_PrintPageType.maintenance THEN // - maintenance
    sCT.pCT^.lPrOutput := EN_PrinterOutput.trash;     // -> eject trash
  ELSIF sPage.lType = EN_PrintPageType.production OR   // - production
        sPage.lSrc = EN_PrintPageSrc.overprod OR       // - over-production
        sPage.lType = EN_PrintPageType.separator THEN  // - separator 
    sCT.pCT^.lPrOutput := EN_PrinterOutput.prod;      // -> production output
  ELSE                                                // - rest
    sCT.pCT^.lPrOutput := EN_PrinterOutput.trash;     // -> eject trash
  END_IF

  lastCreatedCT := sCT;                // successfully produced a CT, store last created CT (necessary for async access within this cycle)
  sCT.pCT^.bCreated := TRUE;
  _created := TRUE;
ELSE
  sCT.setError(EC_Print.ecPMInvGetNext, Src:=_pEcSrc^);   // getNext error
END_IF

(* logging *)
LogLvl(Lvl:=E_LogSeverity.Debug, Msg:=CONCAT('create CT: ', sCT.print(0)));

(*return value*)
CT_Create := _created;]]></ST>
      </Implementation>
    </Method>
    <Method Name="CT_Finish" Id="{439e3f5b-f593-4c3c-9f1c-ba892333a424}">
      <Declaration><![CDATA[METHOD CT_Finish : BOOL
VAR_INPUT
  sCT            : I_CT;               // container to produce
  bSuccess       : BOOL;               // printed OK?
END_VAR
VAR
  bErr           : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[(* checks *)
IF NOT bInitialized THEN RETURN; END_IF                             // exit if no production is active
IF sCT = 0 THEN LogError(aC_Dbg_InvPtrCT); RETURN; END_IF  // log invalid pointer 

// remove from production list
IF NOT prodList.RemoveIndex(prodList.FindIndex(sCT.instance)) THEN
  LogError('CT_Finish: could not find CT in prodList'); 
END_IF

// tag machine errors on CT
IF sCT.pCT^.bValid AND NOT bSuccess THEN  
    sCT.setError(EC_Print.ecPMCTMachineErr, Src:=_pEcSrc^);            // tag CT with: error from machine 
END_IF

// report page done to PU
IF NOT sCT.pCT^.bReported AND sCT.pJob <> 0 THEN
  IF _pPU^.endPage(sCT:=sCT, bSuccess:=bSuccess) THEN
    ;
  ELSE
    sCT.setError(EC_Print.ecPMInvSet, Src:=_pEcSrc^);
  END_IF
END_IF

(* logging *)
IF bSuccess THEN
  LogLvl(Lvl:=E_LogSeverity.Detail, Msg:=CONCAT('successCT: ', sCT.print(0)));
ELSE
  LogLvl(Lvl:=E_LogSeverity.Detail, Msg:=CONCAT('eject  CT: ', sCT.print(0)));  
END_IF

(* return value *)
CT_Finish := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="CT_Produce" Id="{24513523-8391-429c-a6fd-25f8bafd10cf}">
      <Declaration><![CDATA[(*
  Finalize/Produce a CT. Now we shouldhave all necessary information for configuring the CT.
*)
METHOD CT_Produce : BOOL
VAR_INPUT
  sCT                 : I_CT;               // container to produce
END_VAR
VAR
  _produced           : BOOL := FALSE;
  sPage               : ST_PrintPageDef;
  bPresent            : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[(* parameter checks *)
IF NOT bInitialized THEN RETURN; END_IF
IF sCT = 0 THEN LogError(aC_Dbg_InvPtrCT); RETURN; END_IF

sCT.pCT^.bValid           := TRUE;                (* we have all data - CT is valid configured*)

// reject page if no container present or overproduction
sPage := sCT.sPageDef;
bPresent := sCT.pCT^.bProductPresent;
IF NOT bPresent AND sPage.lSrc <> EN_PrintPageSrc.overprod  THEN
  (*   add it to rejected queue (high prio reproduction), we will try to produce this CT in next cycle*)
  IF NOT _pPU^.rejPage(sCT:=sCT) THEN
    LogLvl(Lvl:=E_LogSeverity.Info, aC_Dbg_NotReject);
  END_IF
END_IF

(* logging *)
IF bPresent THEN LogLvl(Lvl:=E_LogSeverity.Detail,  Msg:=CONCAT('produceCT: ', sCT.print(0)));
            ELSE LogLvl(Lvl:=E_LogSeverity.Debug,   Msg:=CONCAT('produceCT: ', sCT.print(0))); END_IF

(* return value*)
CT_Produce := _produced; ]]></ST>
      </Implementation>
    </Method>
    <Property Name="defaultPrData" Id="{66e69f37-58aa-443d-a83c-468610a313a1}">
      <Declaration><![CDATA[PROPERTY defaultPrData : ST_PRINT_SETTING]]></Declaration>
      <Get Name="Get" Id="{22d65e6c-cecd-40fd-8869-faa5495f55dc}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[defaultPrData := _defaultPrintData;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="dynAddPage" Id="{e444ac9f-bf3d-4f42-8d97-959b6c223ef3}">
      <Declaration><![CDATA[METHOD PRIVATE dynAddPage : BOOL
VAR_INPUT
  lType     : EN_PrintPageType; 
  lPageNr   : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _pPU = 0 THEN F_Report_Error(ErrType:=EN_REPORT_ERROR_TYPE.Report_Error_0Pointer); RETURN; END_IF
IF lType <> EN_PrintPageType.maintenance THEN
  LogError('dynAddPage: only supports maintenance pages'); 
END_IF

_pPU^.addPage(lMaintPageNr:=lPageNr);]]></ST>
      </Implementation>
    </Method>
    <Method Name="getNext" Id="{44173359-3a43-4ee9-bef2-8fd31fa38a72}">
      <Declaration><![CDATA[(*
  get next page to be printed.
  :return:    TRUE if success.
*)
METHOD PRIVATE getNext : BOOL               (*get next image to be printed*)
VAR_INPUT
  sCT               : I_CT;                 (*container handle*)
END_VAR
VAR
  bErr              : BOOL;
  lRemainingPages   : UDINT;
END_VAR
VAR_INST
  rTrig_PreStop     : R_TRIG;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT bInitialized THEN RETURN; END_IF                    (* Exit if no production is active*)

// ask pu for a next page
bErr := NOT _pPU^.getPage(sCT:=sCT, lPagesLeft=>lRemainingPages);

// add to producing list
IF NOT prodList.AddItem(Item := sCT.instance) THEN 
  LogError('getNext: could not add CT to prodList');
  bErr := TRUE;
END_IF

getNext := NOT bErr;]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{3c4f7c39-934a-44c4-ba8e-f515cee7c527}">
      <Declaration><![CDATA[METHOD init : BOOL
VAR_INPUT
  pPU     : POINTER TO FB_PU;
  pAdsPU  : POINTER TO ST_ADS_PU;
  // machine specific
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// parameter checks
IF pPU = 0 OR pAdsPU = 0 THEN
  F_Report_Error(ErrType:=EN_REPORT_ERROR_TYPE.Report_Error_0Pointer);
  RETURN;
END_IF

// register external references
_pPU := pPU;
_pAdsPU := pAdsPU;

// init lists
prodList(List:=_prodMem);  // initial call for list setup and init
prodList.Clear();

// init error source
_pEcSrc := ErrorCodes.fbErrCodeMngr.registerErrSource(
  nLocId:=0, 
  aInstPath:=InstanceName, 
  aLocDescr:='PrintManager DMAX'
);
IF _pEcSrc = 0 THEN LogError('could not register error source'); END_IF

//reset setates
bPmActive := FALSE;
lastCreatedCT := 0;

bInitialized := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="JobFinished" Id="{ddf3f1a4-d0df-4de5-a97c-a6501b23d537}">
      <Declaration><![CDATA[METHOD JobFinished : BOOL
VAR_INPUT
  aSeparatorId   : STRING;
END_VAR
VAR
  pHandling      : POINTER TO ST_JobHandling;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Job Handling -----------------------

// NOTE: nothing implemented for now]]></ST>
      </Implementation>
    </Method>
    <Property Name="lastCreated" Id="{de21e749-2b08-434e-b013-35119cd3af60}">
      <Declaration><![CDATA[PROPERTY lastCreated : I_CT]]></Declaration>
      <Get Name="Get" Id="{d88fe05a-a383-4160-8c18-622bb5003c1a}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[lastCreated := lastCreatedCT;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="mJobPause" Id="{6a44bd3a-ab99-45aa-ab8c-fcd698bd816f}" FolderPath="I_PrintStatusClient\">
      <Declaration><![CDATA[METHOD mJobPause
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="mJobStart" Id="{0859a55b-cc7d-43cd-856c-514e909af7ab}" FolderPath="I_PrintStatusClient\">
      <Declaration><![CDATA[METHOD mJobStart
VAR_INPUT
	sJobDef	: I_JobDefinition;
END_VAR
VAR
  lCt     : UDINT;
  sHandling : ST_JobHandling;
  pHandling : POINTER TO ST_JobHandling;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT bInitialized THEN RETURN; END_IF
IF sJobDef = 0 THEN RETURN; END_IF

// start job 
LogLvl(lvl:=E_LogSeverity.Detail, Msg:=F_Concat5('PM start job: ', sJobDef.aJobId, ' (', sJobDef.aJobName, ')'));

// setup PrintManager with new job
_currJobId := sJobDef.aJobId;
_isVarData := sJobDef.isVarData();

// -- init last created
lastCreatedCT := 0;

// -- init defaultPrintData (TODO: not sure why we really need this)
MEMSET(ADR(_defaultPrintData), 0, SIZEOF(_defaultPrintData));   // reset default data

// -- add pre-production pages (product specific) -> maintenance pages
(*FOR lCt := 1 TO lStartEject DO 
  dynAddPage(lType:=EN_PrintPageType.maintenance, 1);
END_FOR*)

bPmActive := TRUE;



]]></ST>
      </Implementation>
    </Method>
    <Method Name="mJobStop" Id="{cbd47bef-8991-4554-a12c-827c7db73263}" FolderPath="I_PrintStatusClient\">
      <Declaration><![CDATA[METHOD mJobStop
VAR_INPUT
	sStatus	: I_JobStatus;
END_VAR
VAR
  eHandlingState  : EN_JobHandlingState;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT bInitialized THEN RETURN; END_IF

(*Stop job*)
LogLvl(lvl:=E_LogSeverity.Detail, Msg:=
  F_Concat7(
    'PM stop job P(',
    TO_STRING(sStatus.iJobProgress.lProductionCount), ') M(',
    TO_STRING(sStatus.iJobProgress.lMaintenanceCount), ') S(',
    TO_STRING(sStatus.iJobProgress.lScrapCount), ')'
  )
);

abortJob();

bPmActive := FALSE;
lastCreatedCT := 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="mProduced" Id="{654fc63e-2f43-4d65-9717-3b144910bc08}" FolderPath="I_PrintStatusClient\">
      <Declaration><![CDATA[METHOD mProduced
VAR_INPUT
	(* current progress*)
	sProgress	: I_JobProgress;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT bInitialized THEN RETURN; END_IF
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Pm_DMAX">
      <LineId Id="3" Count="2" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.abortJob">
      <LineId Id="3" Count="0" />
      <LineId Id="60" Count="1" />
      <LineId Id="44" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.bReady.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.bStarted.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.bStopped.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.CT_Create">
      <LineId Id="3" Count="10" />
      <LineId Id="15" Count="1" />
      <LineId Id="45" Count="0" />
      <LineId Id="17" Count="16" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.CT_Finish">
      <LineId Id="3" Count="30" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.CT_Produce">
      <LineId Id="3" Count="20" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.defaultPrData.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.dynAddPage">
      <LineId Id="3" Count="4" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.getNext">
      <LineId Id="3" Count="9" />
      <LineId Id="36" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.init">
      <LineId Id="3" Count="8" />
      <LineId Id="16" Count="3" />
      <LineId Id="37" Count="5" />
      <LineId Id="36" Count="0" />
      <LineId Id="48" Count="0" />
      <LineId Id="22" Count="2" />
      <LineId Id="28" Count="2" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.JobFinished">
      <LineId Id="3" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.lastCreated.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.mJobPause">
      <LineId Id="4" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.mJobStart">
      <LineId Id="3" Count="8" />
      <LineId Id="16" Count="5" />
      <LineId Id="49" Count="4" />
      <LineId Id="36" Count="4" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.mJobStop">
      <LineId Id="3" Count="12" />
      <LineId Id="28" Count="1" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Pm_DMAX.mProduced">
      <LineId Id="3" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>