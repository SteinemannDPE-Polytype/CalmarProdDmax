<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_PrintPositionDmax" Id="{726347b1-24f3-41dd-8d38-18de7bf18c3b}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_PrintPositionDmax EXTENDS FB_BasePrint IMPLEMENTS I_PrintPosition, I_Sim
VAR_INPUT
  IlEncoderPulse      AT%I* : UDINT;                // Encoder counter intput
  IbEncoderWcState    AT%I* : BOOL;                 // Encoder EL5101 WcState
END_VAR
// ----------------------------------
// configuration
VAR
  // configuration (init)
  _fPrintResolution         : LREAL := 600;         // print(head) resolution in [dpi]
  _fSubPxResolution         : LREAL := 32;          // SubPixel resolution [SubPx], resolution factor (fI_PrintResolution*fI_SubPxResolution=>Output resolution)
  _fTaskCycleTime           : LREAL := 0.002;       // Task cycle time in [s]
  
  _fSpeedMpMin               : LREAL;                // speed in [m/min]
END_VAR
VAR PERSISTENT
  // config values changable during runtime (recipe, visu, property)
  _fEncoderResolution       : LREAL := 2.00475;     // Encoder resolution
  _bEnSpeedComp             : BOOL  := FALSE;
  _fSpeedCompFactor         : LREAL := 0.0;         // TODO: define value
  _fSpeedCompRefSpeed       : LREAL := 0.0;         // TODO: define value
END_VAR
VAR CONSTANT
  fC_MinSpeed               : LREAL := 0.05;
END_VAR
// ---------------------------------
// internal variables
VAR
  // fb inst
  fbPositionMaster          : FB_PositionMaster;

  // internal vars
  _bMoving : BOOL;
  _bStarted : BOOL;
  fEncoderReso              : LREAL;                // used encoder resolution (config + speedcomp)
  rTrigMoving               : R_TRIG;
  // simulation
  _bSim                     : BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[fbPositionMaster.bSim := _bSim;

// set internal used resolution
fEncoderReso := _fEncoderResolution;  

// compensate resolution (if enabled)
IF _bEnSpeedComp THEN
  fEncoderReso := F_EncoderSpeedComp(   
    fMasterSpeed:=fbPositionMaster.fMasterSpeed,
    fResolution:=_fEncoderResolution,
    fSpeedCompFactor:=_fSpeedCompFactor,
    fSpeedCompRefSpeed:=_fSpeedCompRefSpeed
  );
END_IF

// calc position / speed
fbPositionMaster(fResolutionInput:=fEncoderReso, lPosReference:=IlEncoderPulse);

// position status
_bMoving := ABS(fbPositionMaster.fMasterSpeed) >= fC_MinSpeed; 
_fSpeedMpMin := fbPositionMaster.fMasterSpeed * 60;
rTrigMoving(CLK:=bMoving);   // create started trigger output
_bStarted := rTrigMoving.Q OR fbPositionMaster.bSyncReq;
]]></ST>
    </Implementation>
    <Folder Name="I_PrintPosition" Id="{160a1713-28bb-44b2-a2a8-bb85d0f25507}" />
    <Folder Name="I_Sim" Id="{bd8ec4af-70d9-4e9b-8cdc-91b3cc2e54db}" />
    <Property Name="bMoving" Id="{7521517d-ad76-4b9f-8b75-983dc045006f}" FolderPath="I_PrintPosition\">
      <Declaration><![CDATA[(*
  Position is moving.
*)
PROPERTY bMoving : BOOL]]></Declaration>
      <Get Name="Get" Id="{4aca6610-6c03-4327-947a-4962495a26fe}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[bMoving := _bMoving;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="bSimEnable" Id="{30fe551a-b50f-4bf8-9016-a91da26e946e}" FolderPath="I_Sim\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
(*Simulation is enabled set/get*)
PROPERTY bSimEnable : BOOL
]]></Declaration>
      <Get Name="Get" Id="{ba61aeca-91e8-4a46-b50a-3f72adba58be}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[bSimEnable := _bSim;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{bfec738f-7656-45d1-b0ac-a7a5be9bcbcf}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_bSim := bSimEnable;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="bStarted" Id="{a5e6e7c4-c990-4c4c-8c02-08500f77b2ea}" FolderPath="I_PrintPosition\">
      <Declaration><![CDATA[(*
  Started moving event.
*)
PROPERTY bStarted : BOOL]]></Declaration>
      <Get Name="Get" Id="{d0faf2d8-9582-4046-9c77-0e497244db51}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[bStarted := _bStarted;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="fEnoderResolution" Id="{48893527-b087-4406-b1d0-258ef0d4826e}" FolderPath="I_PrintPosition\">
      <Declaration><![CDATA[(*
  Read / Write Encoder resolution.
*)
PROPERTY fEnoderResolution : LREAL]]></Declaration>
      <Get Name="Get" Id="{6cd2975c-4091-4856-aaae-e3fa9a7e3c7d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fEnoderResolution := _fEncoderResolution;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{8933a36c-a8a6-43ad-800f-50ac32d72fc6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_fEncoderResolution := fEnoderResolution;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="fPrintResolution" Id="{48eff3cf-7d99-449a-8526-266094650008}" FolderPath="I_PrintPosition\">
      <Declaration><![CDATA[(*
  print(head) resolution in [dpi]
*)
PROPERTY fPrintResolution : LREAL
]]></Declaration>
      <Get Name="Get" Id="{41eb1aa4-7459-49d4-ab44-e05aefd05fe0}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fPrintResolution := _fPrintResolution;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="fSubPxResolution" Id="{36f7a9b2-ad44-4da7-acb0-b38231b661c0}" FolderPath="I_PrintPosition\">
      <Declaration><![CDATA[(*
   SubPixel resolution [SubPx]
*)
PROPERTY fSubPxResolution : LREAL
]]></Declaration>
      <Get Name="Get" Id="{960643e9-6989-4523-9c9c-eedf45e14680}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fSubPxResolution := _fSubPxResolution;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="getPosition" Id="{f3e3b96b-183e-4641-b112-e525c3bb6c11}" FolderPath="I_PrintPosition\">
      <Declaration><![CDATA[(*
  Get current "default" position.
  [SubPx] as return value and [um] as output
  :return: position in [SubPx] resolution
*)
METHOD getPosition : DINT
VAR_OUTPUT
  nPosition_px       : DINT;                 // Master position in [px]
  fPosition_um       : LREAL;                // Master position in [um]
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[getPosition := fbPositionMaster.lMasterPosition;
nPosition_px := fbPositionMaster.lMasterPosition / LREAL_TO_DINT(_fSubPxResolution);
fPosition_um := fbPositionMaster.fMasterPosition;]]></ST>
      </Implementation>
    </Method>
    <Method Name="getSpeed" Id="{89c2b88d-36bc-48c9-87d7-952cfd236342}" FolderPath="I_PrintPosition\">
      <Declaration><![CDATA[(*
  Get "default" speed [m/s].
  :returns: current speed in [m/s]
*)
METHOD getSpeed : LREAL
VAR_OUTPUT
  fSpeedMpMin : LREAL;  // speed in [m/min]
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[getSpeed    := fbPositionMaster.fMasterSpeed;
fSpeedMpMin := fbPositionMaster.fMasterSpeed*60;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Init" Id="{0edf7ce9-df28-43fb-8910-213c45b50fdb}">
      <Declaration><![CDATA[METHOD Init : BOOL
VAR_INPUT
  fPrintResolution    : LREAL := 600;         // print(head) resolution in [dpi]
  fSubPxResolution    : LREAL := 32;          // SubPixel resolution [SubPx], resolution factor (fI_PrintResolution*fI_SubPxResolution=>Output resolution)
  fTaskCycleTime      : LREAL;                // Task cycle time in [s]
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// store config
_fPrintResolution := fPrintResolution;
_fSubPxResolution := fSubPxResolution;
_fTaskCycleTime   := fTaskCycleTime;

// init fbs
// - position master
fbPositionMaster.init(
  fI_PrintResolution:=fPrintResolution, 
  fI_SubPxResolution:=fSubPxResolution, 
  fI_TaskCycleTime:=fTaskCycleTime, 
  bI_HWEncoder:=FALSE
);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="reset" Id="{2192cbfc-67ef-40f5-ae03-86c2f311383b}">
      <Declaration><![CDATA[METHOD reset
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// reset positioning system
fbPositionMaster.reset();]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_PrintPositionDmax">
      <LineId Id="383" Count="1" />
      <LineId Id="324" Count="14" />
      <LineId Id="382" Count="0" />
      <LineId Id="339" Count="4" />
      <LineId Id="244" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.bMoving.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.bSimEnable.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.bSimEnable.Set">
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.bStarted.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.fEnoderResolution.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.fEnoderResolution.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.fPrintResolution.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.fSubPxResolution.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.getPosition">
      <LineId Id="5" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="41" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.getSpeed">
      <LineId Id="5" Count="0" />
      <LineId Id="13" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.Init">
      <LineId Id="39" Count="0" />
      <LineId Id="34" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="61" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="24" Count="4" />
      <LineId Id="49" Count="0" />
    </LineIds>
    <LineIds Name="FB_PrintPositionDmax.reset">
      <LineId Id="5" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>